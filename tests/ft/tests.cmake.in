#
# This file is part of MCUSim, an XSPICE library with microcontrollers.
#
# Copyright (C) 2017-2019 MCUSim Developers, see AUTHORS.txt for contributors.
#
# MCUSim is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# MCUSim is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.
#

# CMake script to run firmware checks by "make check" command.
file(GLOB_RECURSE FM_TESTS "@CMAKE_CURRENT_BINARY_DIR@/*.ft")

foreach(FM_TEST ${FM_TESTS})
	message(STATUS "[CHECK]: ${FM_TEST}")

	get_filename_component(TEST_WORKING_DIR ${FM_TEST} DIRECTORY)

# -----------------------------------------------------------------------------
# Configure address sanitizer
# -----------------------------------------------------------------------------
	set(WITH_ASAN @WITH_ASAN@)
	if (WITH_ASAN)
		find_program(SYMB llvm-symbolizer)
		message(STATUS "using LLVM symbolizer: ${SYMB}")

		set(ENV{ASAN_OPTIONS} "symbolize=1")
		set(ENV{ASAN_SYMBOLIZER_PATH} ${SYMB})
	endif()

# -----------------------------------------------------------------------------
# Run test
# -----------------------------------------------------------------------------
	execute_process(
		COMMAND ${FM_TEST}
		RESULT_VARIABLE test_res
		WORKING_DIRECTORY ${TEST_WORKING_DIR}
		TIMEOUT 30
	)

	if (NOT "${test_res}" STREQUAL "0")
		message(FATAL_ERROR "failed: ${FM_TEST}")
	else()
		message(STATUS "[END]")
	endif()
endforeach()
